buildscript {
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.5"
    }
}

plugins {
    id "com.github.spotbugs" version "1.6.5"
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"
apply plugin: 'jacoco'

version = "1.1.0"

// Specify JDK version - may vary in different scenarios
sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
// In this section you declare where to find the dependencies of your project
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://maven.aliyun.com/nexus/content/groups/public/"
    }
}

List lombok = [
    "org.projectlombok:lombok:1.16.14"
]

List logger = [
    "org.slf4j:jul-to-slf4j:1.7.10",
    "org.apache.logging.log4j:log4j-api:2.1",
    "org.apache.logging.log4j:log4j-core:2.1",
    "org.apache.logging.log4j:log4j-slf4j-impl:2.1",
    "org.apache.logging.log4j:log4j-web:2.1"
]

def spring_version = "4.1.8.RELEASE"
List spring = [
    "org.springframework:spring-core:$spring_version",
    "org.springframework:spring-beans:$spring_version",
    "org.springframework:spring-context:$spring_version",
    "org.springframework:spring-tx:$spring_version",
    "org.springframework:spring-jdbc:$spring_version",
    "org.springframework:spring-test:$spring_version"
]

// junit test
List junit = [
    "junit:junit:4.12",
    "org.springframework:spring-test:$spring_version"
]

List apache_commons = [
    "org.apache.commons:commons-collections4:4.1",
    "org.apache.commons:commons-lang3:3.5",
    "commons-cli:commons-cli:1.3.1"
]

List jmockit = [
    "org.jmockit:jmockit:1.9"
]

List jackson = [
    "com.fasterxml.jackson.core:jackson-databind:2.8.8.1"
]

List weidentity_contract = [
    "com.webank:weidentity-contract-java:1.+"
]

// In this section you declare the dependencies for your production and test code
List web3sdk_dependencies = [
    "io.netty:netty-all:4.1.15.Final",
    "io.netty:netty-tcnative:2.0.0.Final",
    "com.google.guava:guava:19.0",
    "com.github.fge:json-schema-validator:2.2.6",
    "org.apache.httpcomponents:httpclient:4.5.2",
    "org.bouncycastle:bcprov-jdk15on:1.54",
    "com.lambdaworks:scrypt:1.4.0",
    "com.squareup:javapoet:1.7.0",
    "io.reactivex:rxjava:1.2.4",
    "com.github.jnr:jnr-unixsocket:0.15"
]

// In this section you declare the dependencies for your production and test code
dependencies {
    compile logger, spring, lombok, apache_commons, jackson, web3sdk_dependencies, weidentity_contract
    testCompile logger, spring, lombok, apache_commons, jackson, web3sdk_dependencies, junit, jmockit
    compile files("lib/web3sdk.jar")
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

processResources {
    exclude '**/**'
}

jar {
    destinationDir file('dist/app')
    archiveName project.name + '-' + version + '.jar'

    exclude '**/*.xml'
    exclude '**/*.properties'

    doLast {
        copy {
            from file('src/main/resources/')
            into 'dist/conf'
        }
        copy {
            from configurations.runtime
            into 'dist/lib'
        }
        copy {
            from file('.').listFiles().findAll { File f -> (f.name.endsWith('.bat') || f.name.endsWith('.sh') || f.name.endsWith('.env')) }
            into 'dist'
        }
    }
}

checkstyle {
    ignoreFailures false
    showViolations true
    toolVersion '8.12'
    configProperties.projectDir = project.projectDir
    checkstyleMain.configFile = new File(project.projectDir, '/config/checkstyle/webank_google_checks.xml')
    checkstyleTest.configFile = new File(project.projectDir, '/config/checkstyle/webank_google_checks.xml')
}

tasks.withType(Checkstyle) {
    include '**/*.java'
    exclude '**/contract/*.java'
    reports {
        xml.enabled false
        html.enabled true
    }
}

spotbugs {
    ignoreFailures = true
    showProgress = true
    toolVersion = "3.1.8"
    effort = "max"
    reportLevel = "low"
    excludeFilter = file("${project.rootDir}/config/spotbugs/spotbugs_filter.xml")
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet resources.text.fromFile('config/spotbugs.xsl')
    }
}
